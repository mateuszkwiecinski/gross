import org.gradle.api.initialization.resolve.RepositoriesMode

pluginManagement {
    repositories {
        google {
            content {
                includeGroupByRegex "com\\.android.*"
                includeGroupByRegex "androidx.*"
                includeGroupByRegex "android.arch.*"
                includeGroupByRegex "com\\.google.*"
            }
        }
        gradlePluginPortal {
            content {
                excludeModule("io.github.usefulness", "licensee-for-android")
            }
        }
    }
    if (settings.hasProperty("agpVersion")) {
        def agpVersion = settings.agpVersion
        logger.quiet("AGP version=$agpVersion")
        resolutionStrategy.eachPlugin {
            if (requested.id.id == "com.android.library") {
                useModule("com.android.tools.build:gradle:$agpVersion")
            }
        }
    }
}

dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        google()
        mavenCentral {
            content {
                excludeModule("io.github.usefulness", "licensee-for-android-core")
            }
        }
    }
}
rootProject.name = "io.github.usefulness.sample"

includeBuild("..")
include(':serialization')
include("app")
include(":ui")
